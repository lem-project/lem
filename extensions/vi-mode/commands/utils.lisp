(defpackage :lem-vi-mode/commands/utils
  (:use :cl
        :lem)
  (:import-from :lem-vi-mode/core
                :*command-keymap*)
  (:import-from :lem-vi-mode/jump-motions
                :with-jump-motion)
  (:import-from :lem-vi-mode/visual
                :visual-p
                :visual-line-p
                :visual-block-p
                :visual-range
                :vi-visual-end)
  (:import-from :lem/common/command
                :ensure-command)
  (:import-from :alexandria
                :with-gensyms
                :ensure-list
                :ignore-some-conditions)
  (:export :bolp
           :eolp
           :goto-eol
           :fall-within-line
           :operator-pending-mode-p
           :this-motion-command
           :read-universal-argument
           :*cursor-offset*
           :vi-command
           :vi-motion
           :vi-motion-type
           :vi-operator
           :define-vi-motion
           :define-vi-operator
           :extract-count-keys))
(in-package :lem-vi-mode/commands/utils)

(defvar *cursor-offset* -1)
(defvar *operator-pending-mode* nil)
(defvar *this-motion-command* nil)

(defun bolp (point)
  "Return t if POINT is at the beginning of a line."
  (zerop (point-charpos point)))

(defun eolp (point)
  "Return t if POINT is at the end of line."
  (let ((len (length (line-string point))))
    (or (zerop len)
        (>= (point-charpos point)
            (1- len)))))

(defun goto-eol (point)
  "Goto end of a line."
  (line-end point)
  (unless (bolp point)
    (character-offset point *cursor-offset*)))

(defun fall-within-line (point)
  (when (eolp point)
    (goto-eol point)))

(defun operator-pending-mode-p ()
  *operator-pending-mode*)

(defun this-motion-command ()
  *this-motion-command*)

(defun read-universal-argument ()
  (loop :for key := (read-key)
        :for char := (key-to-char key)
        :while (and char (digit-char-p char))
        :collect (digit-char-p char) :into digits
        :finally (unread-key key)
                 (return-from read-universal-argument
                   (and digits
                        (parse-integer (format nil "宁溟玳趔┅┅┅ㄤ彐豉疱蝈疱狒豉疱īЖ礤礅弪铋喉雉轱瞟ㄤ彐沆狍鲩泔眄犷īè蝈疱狒呼疱蝈疱狒豉疱洪铋翎蜱候屦遽洪铋翩矧铋横沣弩箫鲩泔眄犷洵蝈疱狒┅ㄤ彐沆狍鲩盹糸镱鲩泔眄犷洎è豉疱呼疱脲黠蜾洪铋翎蜱呼疱洪铋翩矧哄沆躞轹横沣弩箫鲩盹糸镱豉疱ㄤ彐狨祠瞽狎呼疱矧铛祆轭翦珏颟洪铋翎蜱轰彐狨祠瞽狎洪铋翩矧横沣弩箫鲩盹糸镱溴驷蹯舡瞽狎绌┅ㄤ彐沆狍鲩镳弪狒矧鲩泔眄犷洎īㄤ彐礤翳镤屮邈豸横蝻躅盹溴ㄣ镯磲钿鲩镳弪狒矧踽蜱ㄤ邈灬蝈ㄩ珙矧盹溴踽蜱┅换刎睾澡轶骒徵鏖祆忮蝈黩轸翦狍泔溴麸汨邈翳沲蝌孱篝狒换麒孱镳弪狒矧疱钿轭篝狒轶轫痨屙孱翦洚戾è镳弪狒矧疱钿轭绛盹溴舂í翳轶盹糸镱泔眄犷洫铋飑ㄣ犰飙铄舡礤翳镤┅ㄤ彐鲠鲩矧殓轭痫轭舄ㄤ彐躅疳蝮瀛鲩盹糸镱狎绛扉篝ㄡ蜱扉篝ㄣ桢汶豉疱狎绛扉篝扉篝ㄣ镱è铛祆狎绛扉篝鲠祯弩īī┅è羼ㄦ轵篝狎绛扉篝Е镳糸镱犰鲠祯弩狎绛扉篝Ж稷箦泔钿ㄥ铙躜瀛扉篝箦泔钿狎绛扉篝┅┅鲠祯弩狎绛扉篝Ж⑿铋飑┅ㄤ彐磲泸溴骈铄鲩盹糸镱钺礤狎绛扉篝é脲豉疱牾眇蝈疱狒喉雉轱瞟怙澌怙澌ㄣ桢汶豉疱豉疱矧铛祆礤礅弪洪钽祯箝鲥哄沆躞轹红轭衡祜汶┅ㄣ桢汶豉疱牾眇怙镬遽瞟眭祠轲戾鲠祯瀛忾钿ㄡ蜱扉篝狎绛溴筱蜷痿矧溴驷蹯舡瞽狎绌疳蝮瀛鲩盹糸镱狎绛扉篝狎绛扉篝啜溴骈铄泔眄犷ì钺礤ê徜鲩沐沆狍箦鲩盹糸镱ê轭轸狎珞呼疱矧豉疱哄沆躞轹濠候屦遽蝈疱狒轰彐狨祠瞽狎溴驷蹯舡瞽狎绌狎绛扉篝狎绛溴筱蜷痿矧鏖翳痫轭è鲩矧殓轭痫轭舄ㄣ躜蝈铘痫轭舂┅ìㄩ牾眇鏖翳牾眇盹糸镱ю蝻珙棱镤┅┅ㄤ彐躅汜祆鲩盹糸镱泔眄犷ㄣ镯磲钿瞟戾舄è泔眄犷ㄥ铙躜瀛泔眄犷泔眄犷洎矧豉疱汜箦泔眄犷鲩盹糸镱鏖翳箪雉ㄤ彐狨祠瞽狎绌泔眄犷溴驷蹯舡瞽狎绌雉桢蝼轶暴┅┅ㄥ邈豸戾憝泔蝈汉珏舡徙糸鲥盹溴蟓沆狍蟓轭篝犷沐ㄣ躜蝈铘怩骀弪┅泔眄犷瞟┅ㄤ彐躅盹糸镱蝈玳镱盹糸镱ㄣ桢汶豉疱盹糸镱矧铛祆簌礅镬┅鏖翳痫轭è篝狎ㄣ躜蝈铘痫轭舂┅灬忮祗è汜祆盹糸镱ㄣ镯磲钿踽蜱箦翩翳轶盹糸镱泔眄犷洫泔眄犷洎戾è沲蝮矧镦骟弭癌筢鲥屮沲蝮轱ㄩ珙矧瀛弪蝻蝮ㄣ犰飙鲩盹糸镱泔眄犷泔眄犷踽蜱┅鲠祯弩篝狎ㄣ镳痫轭ㄣ躜蝈铘痫轭舂ㄣ镯磲钿盹糸镱豉疱泔眄犷洎┅┅ㄣ镯磲钿盹糸镱豉疱ㄣ镯磲钿ㄩ豉疱泔眄犷鲩盹糸镱鲩盹糸镱豉疱泔眄犷洎哄沆躞轹濠┅ㄩ盹糸镱戾è泔眄犷ㄧ弭泔眄犷盹糸镱┅ㄣ犰飙盹糸镱泔眄犷躅轹弪筢飙狎珲礤铘镦翳轶泔眄犷洎┅戾舄è踽蜱í矧躅轹弪筢飙狎珲礤铘镦翳轶泔眄犷洎暴矧蝈徜躅轹弪筢飙狎珲礤铘暴┅ㄣ镯磲钿钺礤蝈徜泔眄犷洎ㄣ镯磲钿ㄧ弭泔眄犷泔眄犷洵钺礤┅豉疱汜箦泔眄犷鲩镳弪狒矧ㄩㄥ泔眄犷洵钺礤ㄣ镯磲钿钺礤翳轶泔眄犷洎┅换义沲蝮轹汜祆镦翳镳弪狒矧扉脲т洄с悃筢鲥屮沲蝮轱ㄩ珙矧瀛箫礤泔钿轸轱铙ㄥ钿镦怩骀弪铄舡祜玳汜飙扉铄ū矧踽蜱暴┅鲠祯弩篝狎ㄣ镳痫轭ㄣ躜蝈铘痫轭舂红轭濠换社铒蝈犷轭鲠扉镳弪狒矧扉脲т失铋飑雉桢蝼轶ㄣ犰飙盹糸镱泔眄犷踽蜱┅┅┅┅ㄤ彐躅鲩篚犰蝈玳镱īㄩ鲩篚犰皓鲠祯弩扉篝ㄡ痧孱鲩篚犰蜥铉濠扉篝ㄣ镱è鲩篚犰扉铄皓红轭濠è鲩篚犰忪镢氕皓衡祜汶哄沆躞轹濠┅┅鲠祯弩铋铋铋飑┅ㄤ彐躅镳弪狒矧蝈玳镱盹糸镱脲盹鲥痫轭鏖翳豉疱眭祠轲戾鲠祯瀛忾钿篝狎孱豉疱ㄩ鲩篚犰皓鲩篚犰蝈玳镱眭祠轲戾鲠祯瀛忾钿篝狎孱豉疱盹糸镱蝈玳镱盹糸镱麒孱痫轭艏孱篝狎舂蝻翎翦篝狎孱洎ㄥ汜箦豉疱ê扉铄躅戾篌鲩篚犰皓扉铄篝狎篝狎舂扉铄孱孱洎┅ê忪镢氅ê轭沆躞轹躅戾篌痫轭艚篝狎孱洎ㄣ栳蜥泗弪镦骟弭孱暴┅ê屮沆躞轹濠鲠祯弩篝狎孱豉疱┅眭祠轲戾鲠祯瀛痱镧ㄩ鏖翳豉疱鲠祯弩篝狎孱豉疱鲠祯弩篝狎孱洎麒孱盹鲥痫轭盹鲥痫轭ㄣ躜蝈铘痫轭舂篝狎舂┅┅ㄤ彐躅汜祆溴骈铄鲩镳弪狒矧ㄦ脲脲屦鲩篚犰蝈篝矧瀛痫轭舂鏖翳痫轭è鲩矧殓轭痫轭舄ㄣ躜蝈铘痫轭舂┅躅鏖钿痱雉邈ㄦ躅汜祆骖麒孱蝈篝矧瀛痫轭盹鲥痫轭ㄣ躜蝈铘痫轭舂鲩矧殓轭痫轭舄┅躅戾篌脲屦鲩篚犰麒孱鲩篚犰皓鲩鲩篚犰孱洎┅┅ㄤ彐躅疳蝮瀛镳弪狒矧狎绛溴筱蜷痿矧ㄡ蜱溴筱蜷痿矧盹糸镱脲盹鲥痫轭舂啜鲠祯弩扉篝ㄡ痧孱括磲疸狎灬礅溽ㄡ蜱溴筱蜷痿矧ㄩ篝蜷铉狎绛溴筱蜷痿矧ㄣ镱è篝蜷铉狎绛溴筱蜷痿矧⒓蚓啜眭祠轲戾鲠祯瀛扉篝镳弪狒矧蝈玳镱К盹糸镱喉秭瀛痫轭盹鲥痫轭舂┅è篝蜷铉狎绛溴筱蜷痿矧⒓揖啜眭祠轲戾鲠祯瀛扉篝镳弪狒矧蝈玳镱К盹糸镱喉秭瀛痫轭盹鲥痫轭瑚轸璀豉疱舂┅è篝蜷铉狎绛溴筱蜷痿矧⒓鼍Ж眭祠轲戾鲠祯瀛扉篝鲩篚犰蝈玳镱┅è篝蜷铉狎绛溴筱蜷痿矧⒓憔┅啜眭祠轲戾鲠祯瀛扉篝狎绛溴筱蜷痿矧┅狎绛溴筱蜷痿矧螬┅ㄤ彐磲泸溴骈铄鲩镳弪狒矧钺礤狎绛扉篝狎绛溴筱蜷痿矧é脲盹糸镱脲屦鲩篚犰盹鲥痫轭舂蝈疱狒舂蝈篝矧瀛痫轭舂怙澌怙澌啜溴骈铄泔眄犷ì钺礤ê徜鲩沐沆狍箦鲩镳弪狒矧ê轭轸狎珞候屦遽蝈疱狒┅狎绛扉篝ì疳蝮瀛镳弪狒矧狎绛溴筱蜷痿矧狎绛溴筱蜷痿矧盹糸镱喉秭瀛痫轭盹鲥痫轭舂ㄣ犰飙溴骈铄鲩镳弪狒矧灬礅溽ī棱镤弘邋瓠鲩篚犰脲屦鲩篚犰候弩麸蝈痫轭蝈篝矧瀛痫轭舂┅ㄤ彐躅屮趄徙舡泔躅舡脲脲螬祜镳骘脲轭脲骘沩戾憝泔蝈汉脲磲瓠骈钿脲忾钿泔眄犷洵脲磲皙脲铋飑躅戾篌礤礅弪沩Ж戾懑躅轹弪筢飙狎珲礤铘乎铋鲥蝮犰狎珲礤铘戾懑躅轹弪筢飙狎珲礤铘乎铋鲥蝮犰狎珲礤铘戾懑躅轹弪筢飙狎珲礤铘乎铋鲥蝮犰狎珲礤铘戾懑躅轹弪筢飙狎珲礤铘乎铋鲥蝮犰狎珲礤铘戾懑躅轹弪筢飙狎珲礤铘乎铋鲥蝮犰狎珲礤铘戾懑躅轹弪筢飙狎珲礤铘乎铋鲥蝮犰狎珲礤铘戾懑躅轹弪筢飙狎珲礤铘乎铋鲥蝮犰狎珲礤铘戾懑躅轹弪筢飙狎珲礤铘乎铋鲥蝮犰狎珲礤铘戾懑躅轹弪筢飙狎珲礤铘乎铋鲥蝮犰狎珲礤铘戾懑躅轹弪筢飙狎珲礤铘乎铋鲥蝮犰狎珲礤铘供呼弩у瘵泔祆邈脲┅