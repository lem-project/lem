(in-package :lem)

(defvar *exit*)

(defun getch ()
  (let* ((code (cl-ncurses:wgetch
                (window-win *current-window*)))
         (char (code-char code)))
    (cond
     ((= code 410)
      (mb-resize)
      (window-adjust-all)
      (getch))
     ((char= char key::ctrl-g)
      (throw 'abort t))
     (t char))))

(add-command 'exit-lem 'exit-lem "C-xC-c")
(defun exit-lem (buffer arg)
  (declare (ignore arg))
  (when (or (not (tblist-any-modif-p))
          (mb-y-or-n-p "Modified buffers exist. Leave anyway"))
    (setq *exit* t)))

(defun self-insert (c arg)
  (buffer-insert-char *current-window* c arg))

(defun execute (keys arg)
  (let ((cmd (find-command keys)))
    (cond
     (cmd
      (funcall cmd *current-window* arg))
     ((or (< 31 (char-code (car keys)))
        (char= key::ctrl-i (car keys)))
      (self-insert (car keys) arg))
     (t
      (mb-write "Key not found")))))

(defun universal-argument ()
  (let ((numlist)
        n)
    (do ((c (mb-read-char "C-u 4")
            (mb-read-char
             (format nil "C-u 狺铛盱轶舂┅铋飑ㄣ镱è汨狎脲汉泗蜢酴箦赳铛盱轶磲疸狎т殓轸汨狎ㄣ镥蜚ㄦ矧磲铋幄íㄩ铛盱轶疳蝮瀛轭翦珏ㄦ矧磲铋狺铛盱轶舂穿┅ъ轶舂┅è犷ㄣ栳蚪＼铛祆铛盱轶舂箦赳铛盱轶ㄡ痧孱铛盱轶扉篝＼┅┅è箦赳ㄤ殓轸汨狎悌箦赳铛盱轶ㄡ痧孱铛盱轶扉篝瞟┅蝈趱蝾鲠祯弩ㄩ铛盱轶疳蝮瀛轭翦珏ㄦ矧磲铋狺铛盱轶舂穿┅┅┅ㄤ彐躅轭瘐舡脲ī戾èㄧ弭汨┅踽蜱麒孱ㄣ栳蚪脲汉泗蜢酴眭祠轲戾鲠祯瀛箦赳ㄣ踽蜱躅轹弪筢飙狎珲礤铘┅ㄩ矧ㄣ栳蚪脲汉泗蜢ㄣ栳蚪脲汉弩汜疱┅鲠祯弩扉篝ㄧ弭汨┅踽蜱戾è怡翦豸娓怡翦ㄣ栳颦泔溴悌┅ㄩ怡翦暴鲠祯弩扉篝悌踽蜱戾è怡翦ㄣ镥蜚磲疸狎с栳颦泔溴ㄣ镱祜镳蝈疱狒ū怡翦螬泔祆邈ㄧ弭汨┅┅Ж鲥泗矧躅箝珙邃怡翦俯┅┅鲠祯弩扉篝ㄡ蝈筲屮艉镢翦趔麸篝蜷铉怡翦螬癌踽蜱┅┅┅ㄤ彐躅戾憝轭轸ㄡ蜱螬ㄣ飙钽躜箦蠛轭轸筱颟ㄣ飙钽躜箦蠛铒邈栾ㄣ飙钽躜箦蠛汊蝈犭ㄣ飙钽躜箦蠛蜥鳗ㄣ飙钽躜箦蠛蝈骝弩瑭鏖钿秣轭轸礅轭轸ㄤ镬轶ㄡ蜱狎珞ㄦ殪瀛镳孱沲蝌孱舡鏖钿秣狎绌┅ㄤ彐躅戾憝骈钺祆辁īㄣ飙钽躜箦蠛孱澉轭┅ㄤ彐躅戾憝磲轭īㄤè屮轸铋飑í屮轸鏖钿秣躔溽翦犰飑麒孱ㄣ狒汨п怙螋眭祠轲戾鲠祯瀛忾钿脲踽蜱ㄩ铕豸脲螬礅沆遽颟ㄥ邈豸脲踽蜱┅铋飑礅黩轸⒘怙螋┅┅ㄤ彐躅戾é蝈篝狎珞戾è痱轭舡汩蜚戾舂鏖翳镳孱骈戾í弪蝻颦秕麴豸⑴乙弦轰轵邈糸镱猴豸瘐洪姝屮轶趔猴鲥蝼蜷翦洪姝滹弩铒舡屮轶恒蝈狒濠躅鏖钿痱雉邈痱镧戾憝轭轸狎珞戾憝磲轭┅戾憝骈钺祆辁濠┅┅