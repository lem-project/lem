(defpackage :lem-vi-mode/commands/utils
  (:use :cl
        :lem)
  (:import-from :lem-vi-mode/jump-motions
                :with-jump-motion)
  (:import-from :lem-vi-mode/visual
                :visual-p
                :visual-line-p
                :visual-block-p
                :apply-visual-range
                :vi-visual-end)
  (:import-from :lem/common/command
                :ensure-command)
  (:import-from :alexandria
                :with-gensyms
                :ensure-list)
  (:export :bolp
           :eolp
           :goto-eol
           :fall-within-line
           :read-universal-argument
           :*cursor-offset*
           :vi-command
           :vi-motion
           :vi-motion-type
           :vi-operator
           :define-vi-motion
           :define-vi-operator))
(in-package :lem-vi-mode/commands/utils)

(defvar *cursor-offset* -1)

(defun bolp (point)
  "Return t if POINT is at the beginning of a line."
  (zerop (point-charpos point)))

(defun eolp (point)
  "Return t if POINT is at the end of line."
  (let ((len (length (line-string point))))
    (or (zerop len)
        (>= (point-charpos point)
            (1- len)))))

(defun goto-eol (point)
  "Goto end of a line."
  (line-end point)
  (unless (bolp point)
    (character-offset point *cursor-offset*)))

(defun fall-within-line (point)
  (when (eolp point)
    (goto-eol point)))

(defun read-universal-argument ()
  (loop :for key := (read-key)
        :for char := (key-to-char key)
        :while (and char (digit-char-p char))
        :collect (digit-char-p char) :into digits
        :finally (unread-key key)
                 (return-from read-universal-argument
                   (and digits
                        (parse-integer (format nil "宁溟玳趔┅┅┅ㄤ彐沆狍鲩泔眄犷īīㄤ彐沆狍鲩盹糸镱鲩泔眄犷洎è豉疱呼疱脲黠蜾洪铋翎蜱呼疱洪铋翩矧哄沆躞轹横沣弩箫鲩盹糸镱豉疱ㄤ彐狨祠瞽狎呼疱矧铛祆轭翦珏颟洪铋翎蜱轰彐狨祠瞽狎洪铋翩矧横沣弩箫鲩盹糸镱溴驷蹯舡瞽狎绌┅ㄤ彐沆狍鲩镳弪狒矧鲩泔眄犷洎īㄤ彐鲠鲩矧殓轭痫轭舄ㄤ彐躅疳蝮瀛鲩盹糸镱狎绛扉篝ㄡ蜱扉篝ㄣ桢汶豉疱狎绛扉篝扉篝ㄣ镱è铛祆狎绛扉篝鲠祯弩īī┅è羼ㄦ轵篝狎绛扉篝Е镳糸镱犰鲠祯弩狎绛扉篝Ж稷箦泔钿ㄥ铙躜瀛扉篝箦泔钿狎绛扉篝┅┅鲠祯弩狎绛扉篝Ж⑿铋飑┅ㄤ彐磲泸溴骈铄鲩盹糸镱钺礤狎绛扉篝é脲豉疱牾眇怙澌怙澌ㄣ桢汶豉疱豉疱矧铛祆礤礅弪洪钽祯箝鲥哄沆躞轹红轭衡祜汶┅ㄣ桢汶豉疱牾眇怙镬遽瞟眭祠轲戾鲠祯瀛忾钿ㄡ蜱扉篝狎绛溴筱蜷痿矧溴驷蹯舡瞽狎绌疳蝮瀛鲩盹糸镱狎绛扉篝狎绛扉篝啜溴骈铄泔眄犷ì钺礤ê徜鲩沐沆狍箦鲩盹糸镱ê轭轸狎珞呼疱矧豉疱哄沆躞轹濠轰彐狨祠瞽狎溴驷蹯舡瞽狎绌狎绛扉篝狎绛溴筱蜷痿矧鏖翳痫轭è鲩矧殓轭痫轭舄ㄣ躜蝈铘痫轭舂┅ìㄩ牾眇鏖翳牾眇盹糸镱ю蝻珙棱镤┅┅ㄤ彐躅汜祆鲩盹糸镱泔眄犷ㄣ镯磲钿瞟戾舄è泔眄犷ㄥ铙躜瀛泔眄犷泔眄犷洎矧豉疱汜箦泔眄犷鲩盹糸镱鏖翳箪雉ㄤ彐狨祠瞽狎绌泔眄犷溴驷蹯舡瞽狎绌雉桢蝼轶暴┅┅ㄣ犰飙泔眄犷泔眄犷瞟┅ㄤ彐鲠鲩镳弪狒矧狎珲礤铘螵铋飑ㄤ彐躅鲩镳弪狒矧蝈玳镱盹糸镱ㄣ桢汶豉疱矧铛祆ㄩ铘彗弪癌┅ㄣ桢汶豉疱盹糸镱矧铛祆簌礅镬┅ㄦ戾è汜祆盹糸镱ㄣ镯磲钿踽蜱戾è沲蝮矧镦骟弭癌ㄩ珙矧瀛弪蝻蝮ㄣ犰飙鲩盹糸镱泔眄犷泔眄犷踽蜱┅┅ㄣ镯磲钿盹糸镱豉疱ㄣ镯磲钿ㄩ豉疱泔眄犷鲩盹糸镱鲩盹糸镱豉疱泔眄犷洎哄沆躞轹濠┅鏖翳痫轭è篝狎ㄣ躜蝈铘痫轭舂┅ㄩ盹糸镱戾è泔眄犷ㄧ弭泔眄犷盹糸镱┅ㄣ犰飙盹糸镱泔眄犷瞟鲠祯弩篝狎ㄣ镳痫轭ㄣ躜蝈铘痫轭舂ㄣ镯磲钿盹糸镱豉疱泔眄犷洎┅戾舄è踽蜱矧蝈徜躅轹弪筢飙狎珲礤铘瞟ㄣ镯磲钿钺礤蝈徜泔眄犷洎ㄣ镯磲钿ㄧ弭泔眄犷泔眄犷洵钺礤┅豉疱汜箦泔眄犷鲩镳弪狒矧ㄩㄥ泔眄犷洵钺礤ㄣ镯磲钿钺礤翳轶泔眄犷洎┅换义沲蝮轹汜祆镦翳镳弪狒矧扉脲т洄с悃鏖翳痫轭è孱ㄣ躜蝈铘痫轭舂┅扉铄镦骟弭孱ū矧踽蜱暴┅鲠祯弩篝狎孱红轭濠换社铒蝈犷轭鲠扉镳弪狒矧扉脲т失铋飑雉桢蝼轶ㄣ犰飙盹糸镱泔眄犷踽蜱鲠祯弩篝狎ㄣ镳痫轭ㄣ躜蝈铘痫轭舂ㄣ镯磲钿盹糸镱豉疱泔眄犷洎┅┅┅┅ㄤ彐躅汜祆鲩镳弪狒矧骖脲盹糸镱脲屦鲩篚犰蝈篝矧瀛痫轭舂ㄦ戾è汜祆鏖翳蝈玳镱ㄦ篝狎孱豉疱麒孱痫轭艏孱篝狎舂蝻翎翦篝狎孱洎ㄥ汜箦豉疱ê扉铄躅戾篌鲩篚犰皓扉铄篝狎篝狎舂扉铄孱孱洎┅ê忪镢氅ê轭沆躞轹躅戾篌痫轭艚篝狎孱洎ㄣ栳蜥泗弪镦骟弭孱暴┅ê屮沆躞轹濠戾è鲩镳弪狒矧狎珲礤铘螵扉篝篝狎孱豉疱┅ㄦ躅汜祆骖篝狎孱豉疱┅┅鏖翳痫轭è鲩矧殓轭痫轭舄ㄣ躜蝈铘痫轭舂┅躅鏖钿痱雉邈ㄩ鲩镳弪狒矧狎珲礤铘螵ㄡ痧禊骖鲩镳弪狒矧狎珲礤铘螵ㄩ鲩篚犰皓ㄡ痧禊鲩篚犰蜥铉灬礅溽篝狎孱洎ㄣ犰飙鏖翳蝈玳镱骖篝狎孱ㄣ镱è鲩篚犰扉铄皓红轭濠è鲩篚犰忪镢氕皓衡祜汶哄沆躞轹濠┅┅眭祠轲戾鲠祯瀛忾钿篝狎孱豉疱鲩镳弪狒矧蝈玳镱盹糸镱ㄣ犰飙鏖翳蝈玳镱骖篝狎孱豉疱┅┅麒孱蝈篝矧瀛痫轭盹鲥痫轭ㄣ躜蝈铘痫轭舂鲩矧殓轭痫轭舄┅躅戾篌脲屦鲩篚犰麒孱鲩篚犰皓鲩鲩篚犰孱洎┅┅┅ㄤ彐磲泸溴骈铄鲩镳弪狒矧钺礤狎绛扉篝é脲盹糸镱脲屦鲩篚犰蝈篝矧瀛痫轭舂怙澌怙澌鏖翳珏铙眢屮趄岘狎珞啜溴骈铄泔眄犷ì钺礤ê徜鲩沐沆狍箦鲩镳弪狒矧┅é镳糸镱犰瞟á孝ㄣ犰飙鲩镳弪狒矧灬礅溽ì括犷狎绛扉篝啜镳糸镱犰泪蜱扉篝┅蝈篝屮趄岘狎珞ㄤ邈灬蝈ㄩ珙矧屮趄岘狎珞┅棱镤喉雉轱К盹糸镱弘邋瓠鲩篚犰脲屦鲩篚犰候弩麸蝈痫轭蝈篝矧瀛痫轭舂┅