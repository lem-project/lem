(defpackage :lem-vi-mode/commands/utils
  (:use :cl
        :lem)
  (:import-from :lem-vi-mode/jump-motions
                :with-jump-motion)
  (:import-from :lem-vi-mode/visual
                :visual-p
                :visual-line-p
                :apply-visual-range
                :vi-visual-end)
  (:import-from :lem/common/command
                :ensure-command)
  (:import-from :alexandria
                :with-gensyms
                :ensure-list)
  (:export :bolp
           :eolp
           :goto-eol
           :fall-within-line
           :read-universal-argument
           :*cursor-offset*
           :vi-command
           :vi-motion
           :vi-motion-type
           :vi-operator
           :define-vi-motion
           :define-vi-operator))
(in-package :lem-vi-mode/commands/utils)

(defun bolp (point)
  "Return t if POINT is at the beginning of a line."
  (zerop (point-charpos point)))

(defun eolp (point)
  "Return t if POINT is at the end of line."
  (let ((len (length (line-string point))))
    (or (zerop len)
        (>= (point-charpos point)
            (1- len)))))

(defun goto-eol (point)
  "Goto end of a line."
  (line-end point)
  (unless (bolp point)
    (character-offset point *cursor-offset*)))

(defun fall-within-line (point)
  (when (eolp point)
    (goto-eol point)))

(defun read-universal-argument ()
  (loop :for key := (read-key)
        :for char := (key-to-char key)
        :while (and char (digit-char-p char))
        :collect (digit-char-p char) :into digits
        :finally (unread-key key)
                 (return-from read-universal-argument
                   (and digits
                        (parse-integer (format nil "宁溟玳趔┅┅┅ㄤ彐沆狍鲩泔眄犷īīㄤ彐沆狍鲩盹糸镱鲩泔眄犷洎è豉疱呼疱脲黠蜾洪铋翎蜱呼疱洪铋翩矧哄沆躞轹横沣弩箫鲩盹糸镱豉疱ㄤ彐狨祠瞽狎呼疱矧铛祆轭翦珏颟洪铋翎蜱轰彐狨祠瞽狎洪铋翩矧横沣弩箫鲩盹糸镱溴驷蹯舡瞽狎绌┅ㄤ彐沆狍鲩镳弪狒矧鲩泔眄犷洎īㄤ彐鲠鲩矧殓轭痫轭舄ㄤ彐鲠沲蝮矧镦骟弭暴ㄤ彐躅疳蝮瀛鲩盹糸镱狎绛扉篝ㄡ蜱扉篝ㄣ桢汶豉疱狎绛扉篝扉篝ㄣ镱è铛祆狎绛扉篝鲠祯弩īī┅è羼ㄦ轵篝狎绛扉篝Е镳糸镱犰鲠祯弩狎绛扉篝Ж稷箦泔钿ㄥ铙躜瀛扉篝箦泔钿狎绛扉篝┅┅鲠祯弩狎绛扉篝Ж⑿铋飑┅ㄤ彐磲泸溴骈铄鲩盹糸镱钺礤狎绛扉篝é脲豉疱牾眇怙澌怙澌ㄣ桢汶豉疱豉疱矧铛祆礤礅弪洪钽祯箝鲥哄沆躞轹红轭濠┅ㄣ桢汶豉疱牾眇怙镬遽瞟眭祠轲戾鲠祯瀛忾钿ㄡ蜱扉篝狎绛溴筱蜷痿矧溴驷蹯舡瞽狎绌疳蝮瀛鲩盹糸镱狎绛扉篝狎绛扉篝啜溴骈铄泔眄犷ì钺礤ê徜鲩沐沆狍箦鲩盹糸镱ê轭轸狎珞呼疱矧豉疱哄沆躞轹濠轰彐狨祠瞽狎溴驷蹯舡瞽狎绌狎绛扉篝狎绛溴筱蜷痿矧鏖翳痫轭è鲩矧殓轭痫轭舄ㄣ躜蝈铘痫轭舂┅ìㄩ牾眇鏖翳牾眇盹糸镱ю蝻珙棱镤┅┅ㄤ彐躅汜祆鲩盹糸镱泔眄犷ㄣ镯磲钿瞟戾舄è泔眄犷ㄥ铙躜瀛泔眄犷泔眄犷洎矧豉疱汜箦泔眄犷鲩盹糸镱鏖翳箪雉ㄤ彐狨祠瞽狎绌泔眄犷溴驷蹯舡瞽狎绌雉桢蝼轶暴┅┅ㄣ犰飙泔眄犷泔眄犷瞟┅ㄤ彐鲠鲩镳弪狒矧狎珲礤铘螵铋飑ㄤ彐磲泸溴骈铄鲩镳弪狒矧钺礤狎绛扉篝é脲盹糸镱脲屦鲩篚犰蝈篝矧瀛痫轭舂怙澌怙澌鏖翳珏铙眢篝狎孱豉疱泔眄犷泔眄犷洵钺礤啜溴骈铄泔眄犷ì钺礤ê徜鲩沐沆狍箦鲩镳弪狒矧┅é镳糸镱犰瞟á孝鏖翳痫轭è鲩矧殓轭痫轭舄ㄣ躜蝈铘痫轭舂┅躅鏖钿痱雉邈ㄩ鲩镳弪狒矧狎珲礤铘螵ㄤ弩趄蹉趱蜷铉忾钿ㄡ钿狎绛扉篝啜镳糸镱犰泪蜱扉篝┅篚怏羼鲩镳弪狒矧狎珲礤铘螵戾铉翳狎绛扉篝┅棱镤鏖翳痫轭è篝狎ㄣ躜蝈铘痫轭舂ì孱ㄣ躜蝈铘痫轭舂┅戾è豉疱ㄩ鲩篚犰扉铄皓红轭哄沆躞轹濠┅ㄩ鲩篚犰皓ㄡ痧禊鲩篚犰蜥铉灬礅溽鲶翎螋鲥钿箦翩篝狎鲶翎螋孱鲥钿┅ㄩ盹糸镱啜痱镧戾è泔眄犷ㄧ弭泔眄犷К盹糸镱┅戾è沲蝮矧镦骟弭癌ㄩ珙矧瀛弪蝻蝮ㄣ犰飙鲩盹糸镱泔眄犷泔眄犷瞟┅麒孱豉疱泔眄犷鲩盹糸镱箦翩豉疱鲩盹糸镱豉疱泔眄犷洎┅盹鲥痫轭孱ㄣ躜蝈铘痫轭舂┅啜戾舄è蝈徜躅轹弪筢飙狎珲礤铘┅ì泔眄犷洵钺礤蝈徜泔眄犷洎ì泔眄犷ㄧ弭泔眄犷泔眄犷洵钺礤┅豉疱汜箦泔眄犷鲩镳弪狒矧换义沲蝮轹汜祆镦翳镳弪狒矧扉脲т洄с悃麒孱ㄥ泔眄犷洵钺礤К钺礤箦翩豉疱红轭濠扉铄镦骟弭孱ū矧暴┅┅雉桢蝼轶戾è沲蝮矧镦骟弭癌ㄩ珙矧瀛弪蝻蝮ㄣ犰飙鲩盹糸镱泔眄犷泔眄犷瞟┅麒孱ㄡ钿豉疱泔眄犷鲩盹糸镱矧ㄥ鲩盹糸镱豉疱泔眄犷洎红轭濠痫轭舣孱ㄣ躜蝈铘痫轭舂┅箦翩豉疱鲩盹糸镱豉疱泔眄犷洎┅盹鲥痫轭孱ㄣ躜蝈铘痫轭舂┅┅┅麒孱痫轭艏孱篝狎舂蝻翎翦篝狎孱洎ㄥ汜箦豉疱ê屮沆躞轹濠ê轭沆躞轹ㄣ栳蜥泗弪镦骟弭孱暴ê扉铄躅戾篌鲩篚犰皓扉铄篝狎篝狎舂扉铄孱孱洎┅戾è鲩镳弪狒矧狎珲礤铘螵扉篝篝狎孱豉疱┅ㄤ弩趄蹉趱蜷铉忾钿ㄡ钿狎绛扉篝啜镳糸镱犰泪蜱扉篝┅篚怏羼鲩镳弪狒矧狎珲礤铘螵戾铉翳狎绛扉篝┅棱镤┅┅括麒孱蝈篝矧瀛痫轭Ж盹鲥痫轭ㄣ躜蝈铘痫轭舂鲩矧殓轭痫轭舄┅括躅戾篌脲屦鲩篚犰Ж麒孱鲩篚犰皓鲩鲩篚犰孱洎┅┅┅┅