;; -*- Mode: LISP; Package: LEM -*-

(in-package :lem)

(export '(split-string
          join
          replace-string
          random-range
          safe-aref))

(defun pdebug (x &optional (file "DEBUG"))
  (with-open-file (out file
                       :direction :output
                       :if-exists :append
                       :if-does-not-exist :create)
    (print x out)))

(defun utf8-bytes (c)
  (cond
   ((<= c #x7f) 1)
   ((<= #xc2 c #xdf) 2)
   ((<= #xe0 c #xef) 3)
   ((<= #xf0 c #xf4) 4)
   (t 1)))

(defvar *eastasian-full*
  (vector
   '(#x01100 #x0115f) '(#x02329 #x0232a) '(#x02e80 #x02e99) '(#x02e9b #x02ef3)
   '(#x02f00 #x02fd5) '(#x02ff0 #x02ffb) '(#x03000 #x0303e) '(#x03041 #x03096)
   '(#x03099 #x030ff) '(#x03105 #x0312d) '(#x03131 #x0318e) '(#x03190 #x031ba)
   '(#x031c0 #x031e3) '(#x031f0 #x0321e) '(#x03220 #x03247) '(#x03250 #x032fe)
   '(#x03300 #x04dbf) '(#x04e00 #x0a48c) '(#x0a490 #x0a4c6) '(#x0a960 #x0a97c)
   '(#x0ac00 #x0d7a3) '(#x0f900 #x0faff) '(#x0fe10 #x0fe19) '(#x0fe30 #x0fe52)
   '(#x0fe54 #x0fe66) '(#x0fe68 #x0fe6b) '(#x0ff01 #x0ff60) '(#x0ffe0 #x0ffe6)
   '(#x1b000 #x1b001) '(#x1f200 #x1f202) '(#x1f210 #x1f23a) '(#x1f240 #x1f248)
   '(#x1f250 #x1f251) '(#x20000 #x2fffd) '(#x30000 #x3fffd)))

(defun wide-table-cmp (key val)
  (cond ((<= (car val) key (cadr val))
         0)
        ((< key (car val))
         -1)
        (t
         1)))

(defun binary-search (vec val cmp-f)
  (labels ((rec (begin end)
                (when (<= begin end)
                  (let* ((i (floor (+ end begin) 2))
                         (result (funcall cmp-f val (aref vec i))))
                    (cond
                     ((plusp result)
                      (rec (1+ i) end))
                     ((minusp result)
                      (rec begin (1- i)))
                     (t
                      (aref vec i)))))))
    (rec 0 (1- (length vec)))))

(defun wide-char-p (c)
  (binary-search *eastasian-full* (char-code c) 'wide-table-cmp))

(defun char-width (c w)
  (cond ((char= c #\tab)
         (+ (* (floor w *tab-size*) *tab-size*) *tab-size*))
        ((or (wide-char-p c) (ctrl-p c))
         (+ w 2))
        (t
         (+ w 1))))

(defun str-width (str &optional (start 0) end)
  (loop :with width := 0
    :for i :from start :below (or end (length str))
    :for c := (aref str i)
    :do (setq width (char-width c width))
    :finally (return width)))

(defun wide-index (str goal &key (start 0))
  (loop
    with w = 0
    for i from start below (length str) by 1
    for c across str do
    (setq w (char-width c w))
    (when (<= goal w)
      (return i))))

(defun split-string (str delim)
  (labels ((f (str acc length)
              (let ((i (position delim str)))
                (if (null i)
                    (values (nreverse (cons str acc))
                            (1+ length))
                    (f (subseq str (1+ i))
                       (cons (subseq str 0 i) acc)
                       (1+ length))))))
    (f str nil 0)))

(defun join (str strings)
  (format nil "窿祜镳烘矧蝈篝猴篝蜷铉恒镬戾泗ㄣ狎蝈篝洪ㄣ潋蝈篝恒镬戾泗篝颟┅ㄤ彐躅蝈痨徙瀛篝蜷铉ㄢ彐矧徭翦篝蜷铉戾è箦狎汨忮骘蝈篝蜷铉┅ㄩ鲠祯弩ㄣ镱汜翦钺翦篝蜷铉篚怏羼篝蜷铉椹徭翦蝈痨徙瀛篝蜷铉忮骘蝈徭翦篚怏羼篝蜷铉ǐ戾铉翳忮骘蝈┅┅舂鲠祯弩篝蜷铉铋飑┅ㄤ彐躅蜥钿镯蜥铉黹磲ǐ黹蜥钿镯ūō磲黹瞟┅┅ㄤ彐躅筢驽狎彐箦镳糸镱犰溴驷蹯舂ㄩ戾铉翳箦瘵ㄡ蝈箦椹溴驷蹯舂ㄤ彐躅篝蜷铉蝈徜汜箦篝蜷铉脲篝狎癌孱洎ㄥ汜箦蝈徜翎忪瀛汜箦蝈徜翎忪濯è乎疸狍濠篝蜷铉躔汜箦篝蜷铉后翎螋篝狎哄钿孱洎è轰秣钽狍濠篝蜷铉滹黝汜箦篝蜷铉后翎螋篝狎哄钿孱洎è洪铞弪吼蝈箦蝣濠篝蜷铉┅ㄤ彐篝蝓泗ㄨ轶麸蝙ê泔铙趄蹉麸ロ犭瀛栝篝矧┅溽翎轭溴ㄤ彐躅磲脲栝篝矧īē磲脲栝篝矧轰狒磲脲狎蜥烘殪飙痫轭翦横潢躞翎忪舂洪钿屮癌ㄤ彐躅徜洵栝篝矧ㄨ轶麸蝙鲥泗矧瘐箬屮翦钿ㄨ轶麸蝙溽翎栝篝矧┅箦翩ㄨ轶麸蝙轭溴栝篝矧戾铉翳ㄨ轶麸蝙溽翎栝篝矧┅ㄤ彐躅痱弼栝篝矧ㄨ轶麸蝙麒孱ㄨ轶麸蝙轭溴栝篝矧┅鲠祯弩ㄡ蝈ㄨ轶麸蝙溽翎栝篝矧ㄤ邈ㄨ轶麸蝙轭溴栝篝矧┅舂┅ㄤ彐躅铄舡栝篝矧ㄨ轶麸蝙麒孱ㄨ轶麸蝙轭溴栝篝矧ū戾铉翳ㄨ轶麸蝙溽翎栝篝矧┅┅鲠祯弩ㄡ蝈ㄨ轶麸蝙溽翎栝篝矧ㄩ钽ㄨ轶麸蝙轭溴栝篝矧┅舂┅ㄤ彐躅忮篝蟓殒ㄦ扉篝翦篝戾è忮篝鲠祯濠ㄢ弩趔┅ㄤ镬轶扉篝戾è筱矧ㄦ躅汜祆骖┅ㄣ镱è矧铒忮篝鲠祯濠ㄦ躅汜祆翦篝筱矧忮篝鲠祯濠箦赳忮篝鲠祯筱矧濠箦赳忮篝扉篝┅è忮篝鲠祯筱矧濠瘐箬忮篝螬┅┅鲠祯弩忮篝忮篝鲠祯濠┅ㄤ彐躅磲殒ㄦ扉篝ㄢ弩趔殒骖扉篝＇京ㄤ彐躅黹瞽殒ㄦ扉篝ㄢ弩趔殒骖扉篝＇缉ㄤ彐躅黼篝é蝈篝狎珞鏖翳秕麴豸麸篝蜷铉螬ㄤ镬轶ㄡ狎珞痱轭螬┅ㄤ彐躅簌礅é蝈篝狎珞鲠祯弩ㄩ铘弪ㄡ痧禊＇黼篝狎珞┅┅ㄤ彐躅黼扉篝ㄩ扉篝扉篝┅