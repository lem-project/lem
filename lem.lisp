(in-package :lem)

(defvar *exit*)
(defvar *universal-argument* nil)

(defun getch ()
  (let* ((code (cl-ncurses:wgetch
                (window-win *current-window*)))
         (char (code-char code)))
    (cond
     ((= code 410)
      (mb-resize)
      (window-adjust-all)
      (getch))
     ((char= char key::ctrl-g)
      (throw 'abort t))
     (t char))))

(defun ungetch (c)
  (cl-ncurses:ungetch (char-code c)))

(define-key *global-keymap* "C-xC-c" 'exit-lem)
(defcommand exit-lem () ()
  (when (or (not (any-modified-buffer-p))
          (y-or-n-p "Modified buffers exist. Leave anyway"))
    (setq *exit* t)))

(define-key *global-keymap* "C-u" 'universal-argument)
(defcommand universal-argument () ()
  (let ((numlist)
        n)
    (do ((c (read-char "C-u 4")
            (read-char
             (format nil "C-u 狺铛盱轶舂┅铋飑ㄣ镱è汨狎脲汉泗蜢酴箦赳铛盱轶磲疸狎т殓轸汨狎ㄣ镥蜚ㄦ矧磲铋幄íㄩ铛盱轶疳蝮瀛轭翦珏ㄦ矧磲铋狺铛盱轶舂穿┅ъ轶舂┅è犷ㄣ栳蚪＼铛祆铛盱轶舂箦赳铛盱轶ㄡ痧孱铛盱轶扉篝＼┅┅è箦赳ㄤ殓轸汨狎悌箦赳铛盱轶ㄡ痧孱铛盱轶扉篝瞟┅躅珏翥悌箦赳躅轹弪筢飙狎珲礤铘ㄩ铛盱轶疳蝮瀛轭翦珏ㄦ矧磲铋狺铛盱轶舂穿蝈趱蝾磲轭篝屦┅┅┅ㄤ彐躅轭瘐舡脲ī戾èㄧ弭汨┅ㄩ矧ㄣ栳蚪脲汉泗蜢ㄣ栳蚪脲汉弩汜疱┅扉篝ㄧ弭汨┅戾è怡翦豸娓怡翦ㄣ栳颦泔溴悌┅ㄩ怡翦暴扉篝悌戾è怡翦ㄣ镥蜚磲疸狎с栳颦泔溴ㄣ镱祜镳蝈疱狒ū怡翦螬泔祆邈ㄧ弭汨┅┅Ж鲥泗矧躅箝珙邃怡翦俯┅┅扉篝ㄡ蝈ㄢ翦蟓麸篝蜷铉怡翦螬癌┅┅┅ㄤ彐躅屮邈豸脲螬戾舄è脲磲沲蝌孱舡脲磲皙ㄣ礓脲磲瓠骈钿泔眄犷脲磲脲螬┅ㄩ沩ㄣ礓汜祆沩躅轹弪筢飙狎珲礤铘脲躅溴姝栾镫脲磲脲螬┅ㄤ彐躅磲轭篝屦ī戾è脲ㄩ铕豸脲螬┅礅沆遽颟ㄥ邈豸脲螬箦赳躅轹弪筢飙狎珲礤铘铋飑┅ㄤ彐躅躅溴骈铄洵脲脲螬戾èㄩ铙弪糸镱脲脲螬┅ㄩㄩ铙弪舡汨狎矧躅轹弪筢飙狎珲礤铘暴礅黩轸⑺妁铒骘躅洧┅┅ㄤ彐躅戾憝轭轸ㄡ蜱螬ㄣ飙钽躜箦蠛轭轸筱颟ㄣ飙钽躜箦蠛铒邈栾ㄣ飙钽躜箦蠛汊蝈犭ㄣ飙钽躜箦蠛蜥鳗ㄣ飙钽躜箦蠛蝈骝弩瑭鏖钿秣轭轸礅轭轸ㄤ镬轶ㄡ蜱狎珞ㄦ殪瀛镳孱狎绌┅ㄤ彐躅戾憝骈钺祆辁īㄣ飙钽躜箦蠛孱澉轭┅ㄤ彐躅戾憝磲轭īㄤè屮轸铋飑í沲蝌腴祆骒徵铋铋飑í灬篝腴祆骒徵铋沲蝌腴祆骒徵┅í屮轸鏖钿秣躔溽翦犰飑麒孱ㄣ狒汨п怙螋磲轭篝屦铋飑礅黩轸⒘怙螋┅┅ㄤ彐躅戾é蝈篝狎珞戾è痱轭舡汩蜚戾舂鏖翳镳孱骈戾í弪蝻颦秕麴豸⑴乙弦轰轵邈糸镱猴豸瘐洪姝屮轶趔猴鲥蝼蜷翦洪姝滹弩铒舡屮轶恒蝈狒濠躅鏖钿痱雉邈痱镧戾憝轭轸狎珞戾憝磲轭┅戾憝骈钺祆辁濠┅┅